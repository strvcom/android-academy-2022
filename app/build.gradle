plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.strv.movies"
        minSdk 23
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", 'TMDB_API_KEY', getEnvString("TMDB_API_KEY", "")
        buildConfigField "String", 'TMDB_USER_NAME', getEnvString("TMDB_USER_NAME", "")
        buildConfigField "String", 'TMDB_PASSWORD', getEnvString("TMDB_PASSWORD", "")

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('./android-academy-2022.keystore')
            keyAlias ALIAS
            keyPassword KEY_PASSWORD
            storePassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {

        release {
            crunchPngs true
            minifyEnabled true //code shrinking, obfuscation and optimization
            shrinkResources true //resource shrinking

            proguardFile '../proguard-rules/proguard-room.pro' //This is not required and it's here as an example
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'


    //Network
    implementation "com.squareup.okhttp3:okhttp:4.9.3"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    implementation "io.coil-kt:coil-compose:2.0.0-rc01"

    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:11.0.1'

    // DataStore
    implementation("androidx.datastore:datastore-preferences:1.0.0")

    // Room database
    implementation("androidx.room:room-runtime:$room_version")
    kapt("androidx.room:room-compiler:$room_version")
    implementation("androidx.room:room-ktx:$room_version")

    //Accompanist
    implementation("com.google.accompanist:accompanist-permissions:0.17.0")
}

static String getEnv(String name, String defaultVal) {
    return System.getenv(name) ?: defaultVal
}

// wrap the result in quotes so it won't be evaluated by the buildConfig
static String getEnvString(String name, String defaultVal) {
    return '"' + getEnv(name, defaultVal) + '"'
}
